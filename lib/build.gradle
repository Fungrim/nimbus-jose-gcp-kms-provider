plugins {
    id 'java-library'
    id 'org.kordamp.gradle.jandex' version '0.11.0'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
    id 'com.diffplug.spotless' version '6.2.1'
    id 'maven-publish'
    id 'signing'
}

ext {
    jupiterVersion = '5.7.2'
    nimbusVersion = '9.15.2'
    gcpPlatformVersion = '24.0.0'
    guavaVersion = '31.0.1-jre'
}

spotless {
  java {
    eclipse()          
    importOrder()
    removeUnusedImports()
    licenseHeaderFile 'misc/license-header.txt'
    indentWithSpaces(4)
  }
}

testSets {
    itest
}

repositories {
    mavenCentral()
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'gcp-kms-nimbus-provider'
            from components.java
            pom {
                name = 'GCP KMS Nimbus Provider'
                description = 'A library that provides JWS signers and verifiers based on Google Cloud KMS for Nimbus JOSE.'
                url = 'https://fungrim.github.io/nimbus-jose-gcp-kms-provider/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'fungrim'
                        name = 'Lars J. Nilsson'
                        email = 'fungrim+github@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Fungrim/nimbus-jose-gcp-kms-provider.git'
                    developerConnection = 'scm:git:ssh://github.com/Fungrim/nimbus-jose-gcp-kms-provider.git'
                    url = 'http://github.com/Fungrim/nimbus-jose-gcp-kms-provider/tree/maste'
                }
            }
        }
    }
}

signing {
     sign publishing.publications.maven
}

dependencies {
    // internals 
    api "com.google.guava:guava:${guavaVersion}"

    // nimbus
    api "com.nimbusds:nimbus-jose-jwt:${nimbusVersion}"

    implementation "org.bouncycastle:bcprov-jdk15on:1.68"
    implementation "org.bouncycastle:bcpkix-jdk15on:1.68"
    
    // gpp kms
    implementation platform("com.google.cloud:libraries-bom:${gcpPlatformVersion}")
    api "com.google.cloud:google-cloud-kms"

    // testing
    testImplementation "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
    testImplementation 'org.mockito:mockito-core:4.2.0'

    // itest
    itestImplementation "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
    itestImplementation 'org.mockito:mockito-core:4.2.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('itest') {
    useJUnitPlatform()
}